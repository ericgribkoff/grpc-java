plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.27.0-SNAPSHOT' // CURRENT_GRPC_VERSION

dependencies {
    implementation "io.grpc:grpc-xds:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"
}

startScripts.enabled = false

task helloWorldXdsClient(type: CreateStartScripts) {
    mainClassName = 'io.grpc.examples.xds.HelloWorldXdsClient'
    applicationName = 'hello-world-xds-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task helloWorldXdsServer(type: CreateStartScripts) {
    mainClassName = 'io.grpc.examples.xds.HelloWorldXdsServer'
    applicationName = 'hello-world-xds-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(helloWorldXdsClient)
    from(helloWorldXdsServer)
    fileMode = 0755
}
